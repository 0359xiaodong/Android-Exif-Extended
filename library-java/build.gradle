
apply plugin: 'android-library'

dependencies {
    compile 'com.android.support:support-v4:19.0.+'
    compile 'it.sephiroth.android.library.media:android-exif-extended-native:2.97'
}

android {
    compileSdkVersion 19
    buildToolsVersion "18.1.1"

    defaultConfig {
        versionCode 1
        versionName version
        minSdkVersion 9
        targetSdkVersion 19
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }
}

project.ext {
    outJar = rootProject.name + '-' + version + '.jar'
}


def generatePom() {
    def pom = pom{
        project {
            groupId groupId
            artifactId project.name
            version version
            description 'Exif extended library for Android, based on jhead c library (http://www.sentex.net/~mwandel/jhead/) '
            inceptionYear '2013'
            url 'git@github.com:sephiroth74/Android-Exif-Extended.git'

            licenses {
                license {
                    name 'Public Domain Software'
                    distribution 'repo'
                }
            }

            scm {
                url                 "scm:git@github.com:sephiroth74/Android-Exif-Extended.git"
                connection          "scm:git@github.com:sephiroth74/Android-Exif-Extended.git"
                developerConnection "scm:git@github.com:sephiroth74/Android-Exif-Extended.git"
            }


            developers {
                developer {
                    id 'sephiroth74'
                    name 'Alessandro Crugnola'
                    email 'alessandro.crugnola@gmail.com'
                    url 'http://blog.sephiroth.it'
                    roles {
                        role 'author'
                        role 'developer'
                    }
                }
            }
        }
    }
}

task clearJar(type: Delete) {
    delete "build/libs/" + project.ext.outJar
}

task makeJar(type: Copy) {
    from('build/bundles/release/')
    into('build/libs/')
    include('classes.jar')
    rename ('classes.jar', project.ext.outJar)
}

task writePom {
    ext.pomXml = file("pom.xml")
    inputs.file(project.buildFile)
    outputs.file(pomXml)
    doLast {
        generatePom().writeTo(pomXml)
    }
}

uploadArchives {
    doFirst {
        repositories.mavenDeployer.pom = generatePom()
    }

    repositories.mavenDeployer {
        repository(url: "file://$System.env.HOME/.m2/repository/")
    }
}

clean.dependsOn 'clearJar'
build.dependsOn = ['writePom','makeJar']

